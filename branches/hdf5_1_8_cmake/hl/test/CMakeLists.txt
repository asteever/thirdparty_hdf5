# --------------------------------------------------------------------
# Notes: When creating unit test executables they should be prefixed 
# with "hl_". This allows for easier filtering of the test suite when
# using ctest. An example would be
#  ctest -R hl_
# which would only run the High Level based unit tests.
# --------------------------------------------------------------------

PROJECT( HDF5_HL_TEST)
cmake_minimum_required(VERSION 2.6)

#-----------------------------------------------------------------------------
# Setup output Directories 
#-----------------------------------------------------------------------------
IF(NOT HDF5_EXTERNALLY_CONFIGURED)
  SET (CMAKE_RUNTIME_OUTPUT_DIRECTORY
    ${PROJECT_BINARY_DIR}/bin CACHE PATH "Single Directory for all Executables."
  )
  SET (CMAKE_LIBRARY_OUTPUT_DIRECTORY
    ${PROJECT_BINARY_DIR}/bin CACHE PATH "Single Directory for all Libraries"
  )
  SET (CMAKE_ARCHIVE_OUTPUT_DIRECTORY
    ${PROJECT_BINARY_DIR}/bin CACHE PATH "Single Directory for all static libraries."
  )
ENDIF(NOT HDF5_EXTERNALLY_CONFIGURED)

INCLUDE_DIRECTORIES( ${HDF5_TEST_SOURCE_DIR} )


# --------------------------------------------------------------------
#  Macro used to add a unit test
# --------------------------------------------------------------------
macro (HL_ADD_TEST hl_name files)
    ADD_EXECUTABLE(hl_${hl_name} ${hl_name}.c )
    TARGET_LINK_LIBRARIES(hl_${hl_name} 
                            ${HDF5_HL_LIB_NAME} 
                            ${HDF5_LIB_NAME}
                            ${HDF5_TEST_LIB_NAME} )
    ADD_TEST(hl_${hl_name} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/hl_${hl_name})
    
    #-- Copy the necessary files.
    FOREACH ( h5_file ${files} )
       SET (dest "${HDF5_HL_TEST_BINARY_DIR}/${h5_file}")
       #MESSAGE(STATUS " Copying HL Test File ${h5_file} to ${dest}")
       ADD_CUSTOM_COMMAND (
         TARGET     hl_${hl_name} 
         PRE_BUILD
         COMMAND    ${CMAKE_COMMAND}
         ARGS       -E copy_if_different ${HDF5_HL_TEST_SOURCE_DIR}/${h5_file} ${dest}
     )
     
    ENDFOREACH ( h5_file ${HL_REFERENCE_TEST_FILES} )
endmacro(HL_ADD_TEST hl_name)

HL_ADD_TEST( test_ds "dsdata.txt;dslat.txt;dslon.txt;test_ds_be.h5;test_ds_le.h5")
HL_ADD_TEST( test_image "image8.txt;sepia.pal;earth.pal;image24pixel.txt;image24plane.txt;usa.wri")
HL_ADD_TEST( test_lite "dtype_file.txt")
HL_ADD_TEST( test_packet "")
HL_ADD_TEST( test_table "test_table_be.hdf5;test_table_cray.hdf5;test_table_le.hdf5")


# --------------------------------------------------------------------
# This executable is used to generate test files for the test_ds test.
# It should only be run during development when new test files are needed
# --------------------------------------------------------------------
ADD_EXECUTABLE(hl_gen_test_ds gen_test_ds.c )
TARGET_LINK_LIBRARIES(hl_gen_test_ds 
                        ${HDF5_HL_LIB_NAME} 
                        ${HDF5_LIB_NAME}
                        ${HDF5_TEST_LIB_NAME} )

                        